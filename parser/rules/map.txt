<program> ===> <otherFunctions> <mainFunction>
<mainFunction> ===> TK_MAIN <stmts> TK_END
<otherFunctions> ===> <function> <otherFunctions>
<otherFunctions> ===> eps
<function> ===> TK_FUNID <input_par> <output_par> TK_SEM <stmts> TK_END
<input_par> ===> TK_INPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
<output_par>===> TK_OUTPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
<output_par>===> eps
<parameter_list> ===> <dataType> TK_ID <remaining_list>
<dataType> ===> <primitiveDatatype>
<dataType> ===> <constructedDatatype>
<primitiveDatatype> ===> TK_INT
<primitiveDatatype> ===> TK_REAL
<constructedDatatype> ===> TK_RECORD TK_RECORDID
<remaining_list> ===> TK_COMMA <parameter_list>
<remaining_list> ===> eps
<stmts> ===> typeDefinitions <declarations> <otherStmts> <returnStmt>
<typeDefinitions> ===> <typeDefinition> <typeDefinitions>
<typeDefinitions> ===> eps
<typeDefinition> ===> TK_RECORD TK_RECORDID <fieldDefinitions> TK_ENDRECORD TK_SEM
<fieldDefinitions> ===> <fieldDefinition> <fieldDefinition> <moreFields>
<fieldDefinition> ===> TK_TYPE <primitiveDatatype> TK_COLON TK_FIELDID TK_SEM
<moreFields> ===> <fieldDefinition> <moreFields>
<moreFields> ===> eps
<declarations> ===> <declaration> <declarations>
<declarations> ===> eps
<declaration> ===> TK_TYPE <dataType> TK_COLON TK_ID <global_or_not> TK_SEM
<global_or_not> ===> TK_COLON TK_GLOBAL
<global_or_not> ===> eps
<otherStmts> ===> <stmt> <otherStmts>
<otherStmts> ===> eps
<stmt> ===> <assignmentStmt>
<stmt> ===> <iterativeStmt>
<stmt> ===> <conditionalStmt>
<stmt> ===> <ioStmt>
<stmt> ===> <funCallStmt>
<assignmentStmt> ===> <singleOrRecId> TK_ASSIGNOP <arithmeticExpression> TK_SEM
<singleOrRecId> ===> TK_ID
<singleOrRecId> ===> TK_RECORDID TK_DOT TK_FIELDID
<iterativeStmt> ===> TK_WHILE TK_OP <booleanExpression> TK_CL stmt otherStmts TK_ENDWHILE
<funCallStmt> ===> <outputParameters> TK_CALL TK_FUNID TK_WITH TK_PARAMETERS <inputParameters> TK_SEM
<outputParameters> ===> TK_SQL idList TK_SQR TK_ASSIGNOP
<outputParameters> ===> eps
<inputParameters> ===> TK_SQL <idList> TK_SQR
<ioStmt> ===> TK_READ TK_OP <singleOrRecId> TK_CL TK_SEM
<ioStmt> ===> TK_WRITE TK_OP <allVar> TK_CL TK_SEM
<allVar> ===> TK_ID
<allVar> ===> TK_NUM
<allVar> ===> TK_RNUM
<allVar> ===> TK_RECORDID
<returnStmt> ===> TK_RETURN <optionalReturn> TK_SEM
<optionalReturn> ===> TK_SQL <idList> TK_SQR
<optionalReturn> ===> eps
<idList> ===> TK_ID <more_ids>
<more_ids> ===> TK_COMMA <idList>
<more_ids> ===> eps
<conditionalStmt> ===> TK_IF TK_OP <booleanExpression> TK_CL TK_THEN <stmt> <otherStmts> elsePart
<elsePart> ===> TK_ELSE <stmt> <otherStmts> TK_ENDIF
<elsePart> ===> TK_ENDIF
<arithmeticExpression> ===> <arithmeticExpression1>
<arithmeticExpression> ===> <arithmeticExpression2>
<arithmeticExpression1> ===> TK_RECORDID <addOrSub> TK_RECORDID
<arithmeticExpression1> ===> TK_RECORDID mulOrDiv arithmeticExpression2
<arithmeticExpression2> ===> <term> <notExp>
<notExp> ===> <addorSub> <term> <notExp>
<notExp> ===> eps
<term> ===> <factor> <notTerm>
<notTerm> ===> <mulOrDiv> <factor> <notTerm>
<notTerm> ===> eps
<factor> ===> TK_OP <arithmeticExpression2> TK_CL
<factor> ===> <all>
mulOrDiv ===> TK_MUL
mulOrDiv ===> TK_DIV
addOrSub ===> TK_PLUS
addOrSub ===> TK_MINUS
<all> ===> TK_NUM
<all> ===> TK_RNUM
<all> ===> TK_ID
<all> ===> TK_RECORDID TK_DOT TK_FIELDID
<booleanExpression> ===> TK_OP <booleanExpression> TK_CL logicalOp TK_OP booleanExpression TK_CL
<booleanExpression> ===> var relationalOp var
booleanExpression ===> TK_NOT TK_OP booleanExpression TK_CL
<var> ===> TK_ID
<var> ===> TK_NUM
<var> ===> TK_RNUM
<logicalOp> ===> TK_AND
<logicalOp> ===> TK_OR
<relationalOp> ===> TK_LT
<relationalOp> ===> TK_LE
<relationalOp> ===> TK_EQ
<relationalOp> ===> TK_GT
<relationalOp> ===> TK_GE
<relationalOp> ===> TK_NE